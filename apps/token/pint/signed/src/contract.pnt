use std::lib::Secp256k1Signature;
use std::lib::Secp256k1PublicKey;
use std::lib::PredicateAddress;

interface Token {
    storage {
        balances: (b256 => int),
        nonce: (b256 => int),
        token_name: b256,
        token_symbol: b256,
        decimals: int,
    }

    predicate TransferI {
        pub var key: b256;
        pub var to: b256;
        pub var amount: int;
    }

    predicate BurnI {
        pub var key: b256;
        pub var amount: int;
    }

    predicate MintI {
        pub var key: b256;
        pub var amount: int;
        pub var decimals: int;
    }
    
    predicate CancelI {
        pub var key: b256;
    }
}

// The interface of dependent constraints
interface OtherConstraints {
    predicate Constraints {
        pub var key: b256;
    }
}

enum TransferMode = All | Key | KeyTo | KeyAmount;

predicate Transfer {
    // The token address
    pub var token: PredicateAddress;

    interface T = Token(token.contract);
    predicate I = T::TransferI(token.addr);
    
    var sig: std::lib::Secp256k1Signature;
    @common({I::key, I::to, I::amount}; T; I; token; sig);
}

predicate TransferWith {
    // The token address
    pub var token: PredicateAddress;

    var mode: TransferMode;

    interface T = Token(token.contract);
    predicate I = T::TransferI(token.addr);
    
    var sig: Secp256k1Signature;
    
    state nonce = T::storage::nonce[I::key];
    
    @check_signature_if(mode == TransferMode::All; {I::key, I::to, I::amount, nonce', token, constraints}; sig; I::key);
    @check_signature_if(mode == TransferMode::Key; {I::key, nonce', token, constraints}; sig; I::key);
    @check_signature_if(mode == TransferMode::KeyTo; {I::key, I::to, nonce', token, constraints}; sig; I::key);
    @check_signature_if(mode == TransferMode::KeyAmount; {I::key, I::amount, nonce', token, constraints}; sig; I::key);
    
    var constraints: std::lib::PredicateAddress;
    interface Other = OtherConstraints(constraints.contract);
    predicate O = Other::Constraints(constraints.addr);
    constraint O::key == I::key;
}

predicate Burn {
    // The token address
    pub var token: PredicateAddress;

    interface T = Token(token.contract);
    predicate I = T::BurnI(token.addr);
    
    var sig: std::lib::Secp256k1Signature;
    @common({I::key, I::amount}; T; I; token; sig);
}

predicate Mint {
    // The token address
    pub var token: PredicateAddress;

    interface T = Token(token.contract);
    predicate I = T::MintI(token.addr);
    
    var sig: std::lib::Secp256k1Signature;
    @common({I::key, I::amount, I::decimals}; T; I; token; sig);
}

predicate Cancel {
    // The token address
    pub var token: PredicateAddress;

    interface T = Token(token.contract);
    predicate I = T::CancelI(token.addr);

    var sig: std::lib::Secp256k1Signature;
    @common(I::key; T; I; token; sig);
}

macro @common($data, $token, $predicate, $token_addr, $sig) {
    state nonce = $token::storage::nonce[$predicate::key];
    constraint @verify_key({$data, nonce', $token_addr}; $sig; $predicate::key);
}

macro @check_signature_if($cond, $data, $sig, $key) {
    constraint ($cond && @verify_key($data; $sig; $key)) || true;
}

macro @verify_key($data, $sig, $key) {
    __sha256(__recover_secp256k1(__sha256($data), $sig)) == $key

}